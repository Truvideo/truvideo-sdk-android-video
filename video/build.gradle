plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.devtools.ksp'
}

def propertiesFile = rootProject.file("video/src/main/assets/version.properties")
def properties = new Properties()
properties.load(new FileInputStream(propertiesFile))

android {
    namespace 'com.truvideo.sdk.video'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionName properties.getProperty("versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    ksp {
        arg('room.schemaLocation', "$projectDir/schemas")
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }

    packaging {
        resources {
            it.excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    flavorDimensions = ['environments']

    productFlavors {
        dev{
            dimension 'environments'
        }
        beta {
            dimension 'environments'
        }
        rc {
            dimension 'environments'
        }
        prod {
            dimension 'environments'
        }
    }
}

dependencies {
    implementation 'androidx.startup:startup-runtime:1.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0'

    // Compose
    implementation platform('androidx.compose:compose-bom:2024.06.00')
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-graphics"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.material:material-icons-extended"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    // Truvideo SDK Common
    var commonVersion = properties.getProperty("commonVersion")
    implementation("com.github.truvideo:truvideo-sdk-android-common:$commonVersion")

    // Truvideo SDK Components
    implementation 'com.github.Truvideo:truvideo-sdk-android-components:0.1.17'

    // Krisp
    implementation 'com.github.Truvideo:truvideo-sdk-android-krisp:1.0.0'

    // FFmpeg
    implementation 'com.arthenica:mobile-ffmpeg-min-gpl:4.4'

    // Room
    implementation "androidx.room:room-runtime:2.6.1"
    annotationProcessor "androidx.room:room-compiler:2.6.1"
    ksp "androidx.room:room-compiler:2.6.1"

    // Exoplayer
    implementation "androidx.media3:media3-exoplayer:1.3.1"
    implementation "androidx.media3:media3-ui:1.3.1"

    // Test
    testImplementation "androidx.test:core:1.6.1"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:5.4.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.0.0"
    testImplementation "io.mockk:mockk:1.13.5"
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                setGroupId 'com.github.Truvideo'
                setArtifactId 'truvideo-sdk-android-video-local'
                version android.defaultConfig.versionName

                def environment = properties.getProperty("environment")
                switch (environment) {
                    case 'dev':
                        artifact bundleDevReleaseAar
                        break

                    case 'beta':
                        artifact bundleBetaReleaseAar
                        break

                    case 'rc':
                        artifact bundleRcReleaseAar
                        break

                    case 'prod':
                        artifact bundleProdReleaseAar
                        break
                }

                pom.withXml {
                    //noinspection GroovyImplicitNullArgumentCall
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}